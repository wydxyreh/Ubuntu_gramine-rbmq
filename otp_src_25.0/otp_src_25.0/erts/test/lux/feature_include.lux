[doc Tests regarding feature enabling with include files]

[include features.luxinc]

[invoke setup]

[newshell cmds]
    !export OTP_TEST_FEATURES=true
    ?SH-PROMPT:
    # Do not use erlc server as it seems that under "sometimes" new
    # servers are added and left lingering
    !export ERLC_USE_SERVER=$erlcserver
    ?SH-PROMPT:
    # Ensure that the feature unless_expr is enabled in the
    # include file and generates an error
    [invoke compile f_include_1.erl ""]
    ??syntax error before
    [invoke notok]

[shell cmds]
    # Ensure that the macro knows that unless_expr is enabled in the
    # include file
    [invoke compile f_include_2.erl ""]
    [invoke ok]

[newshell erl]
    !export OTP_TEST_FEATURES=true
    ?SH-PROMPT:
    [invoke start-erl "-enable-feature unless_expr"]
    !l(f_include_2).
    ??{module,f_include_2}
    ?$ERLPROMPT

    !f_include_2:foo(2).
    ?active
    ?$ERLPROMPT

[shell cmds]
    [invoke compile f_include_3.erl "-Dend_prefix"]
    ?feature directive not allowed after anything interesting
    [invoke notok]

[shell cmds]
    [invoke compile f_include_1.erl "-Dend_include"]
    ?feature directive not allowed after anything interesting
    [invoke notok]

[shell cmds]
    [invoke compile f_include_maybe.erl "-Denable_maybe=0"]
    [invoke ok]

[shell erl]
    !l(f_include_maybe).
    ??{module,f_include_maybe}
    ?$ERLPROMPT
    !f_include_maybe:foo().
    ??{conditional,on,until}
    ?$ERLPROMPT

[shell cmds]
    [invoke compile f_include_maybe.erl "-enable-feature while_expr"]
    ??input/maybe.hrl
    ??syntax error before
    [invoke notok]

    [invoke compile f_include_maybe.erl "-Denable_maybe=1"]
    ??input/f_include_maybe.erl
    ??syntax error before
    [invoke notok]

    [invoke compile f_include_maybe.erl "-enable-feature while_expr -Denable_maybe=2"]
    [invoke ok]

[shell erl]
    [invoke quit-erl]

    [invoke start-erl "-enable-feature while_expr"]
    !l(f_include_maybe).
    ??{module,f_include_maybe}
    ?$ERLPROMPT
    !f_include_maybe:foo().
    ??{conditional,off,none}
    ?$ERLPROMPT

[cleanup]
    [invoke no-dump]
    # done
