[doc Testing of enabling features on command line]

[include features.luxinc]

[invoke setup]

[newshell erl]
    !export OTP_TEST_FEATURES=true
    ?SH-PROMPT:
    -undefined function
    [invoke start-erl ""]

    # Verify that the known test features are returned
    [invoke same-members "erl_features:all()" "[maps,cond_expr,while_expr,ifn_expr,ifnot_expr,unless_expr]"]

    # Verify that we get an error if trying to get information about
    # an unknown feature.
    !erl_features:info(loop_expr).
    ?error.*invalid_feature
    ?unknown feature
    ?$ERLPROMPT

    # Verify that we get a map with info for a known feature
    !IMap = erl_features:info(maps).
    ?$ERLPROMPT
    !is_map(IMap).
    ?true
    ?$ERLPROMPT
    !lists:all(fun(K) -> is_map_key(K, IMap) end, [status,type,description,short,experimental]).
    ?true
    ?$ERLPROMPT

    [invoke same-members "erl_features:enabled()" "[maps,cond_expr]"]

    [invoke quit-erl]

    [invoke start-erl "-enable-feature ifn_expr"]

    [invoke same-members "erl_features:enabled()" "[maps,cond_expr,ifn_expr]"]

    [invoke quit-erl]

    [invoke start-erl "-enable-feature ifn_expr -enable-feature while_expr"]

    [invoke same-members "erl_features:enabled()" "[maps,cond_expr,while_expr,ifn_expr]"]

    [invoke quit-erl]


    [invoke start-erl "-enable-feature ifn_expr while_expr"]

    [invoke same-members "erl_features:enabled()" "[cond_expr,maps,while_expr,ifn_expr]"]

    [invoke quit-erl]

    [invoke start-erl "-enable-feature ifn_expr while_expr -enable-feature ifnot_expr"]

    [invoke same-members "erl_features:enabled()" "[cond_expr,maps,while_expr,ifn_expr,ifnot_expr]"]

    [invoke quit-erl]

    # Enabling the same feature twice does not hurt
    [invoke start-erl "-enable-feature ifn_expr while_expr -enable-feature ifnot_expr ifn_expr"]

    [invoke same-members "erl_features:enabled()" "[cond_expr,maps,while_expr,ifn_expr,ifnot_expr]"]

    [invoke quit-erl]

    # Now for some bad stuff
    # Enabling a feature that doen't exist
    # FIXME shouldn't this give an error?  Yes, but here we have a
    # problem since we have a lazy init of the feature set.
    [invoke start-erl "-enable-feature no_ftr"]

    [invoke same-members "erl_features:enabled()" "[cond_expr,maps]"]

    [invoke quit-erl]

    [invoke start-erl "-disable-feature cond_expr"]

    [invoke same-members "erl_features:enabled()" "[maps]"]
    # FIXME Need to check that keywords have changed as well!
    [invoke quit-erl]

    [invoke start-erl "-disable-feature all"]
    !erl_features:enabled().
    ??[]
    ?$ERLPROMPT

    [invoke quit-erl]

[cleanup]
    [invoke no-dump]
    # done
