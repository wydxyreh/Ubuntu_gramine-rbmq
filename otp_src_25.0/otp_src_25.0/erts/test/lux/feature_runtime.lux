[doc Test general working of runtime state of features]

[include features.luxinc]

[invoke setup]

[newshell erl]
    !export OTP_TEST_FEATURES=true
    ?SH-PROMPT:
    -undefined function
    [invoke start-erl ""]

    [invoke same-members "erl_features:enabled()" "[cond_expr,maps]"]

    !erl_features:keywords().
    ??[]
    ?$ERLPROMPT

    [invoke quit-erl]
    [invoke start-erl "-enable-feature while_expr"]

    !erl_features:keywords().
    ??['while','until']
    ?$ERLPROMPT

    [invoke quit-erl]
    [invoke start-erl "-enable-feature ifn_expr -enable-feature while_expr"]

    [invoke same-members "erl_features:enabled()" "[cond_expr,maps,ifn_expr,while_expr]"]

    !erl_features:keywords().
    ??['while','until','ifn']

    [invoke quit-erl]
    [invoke start-erl "-enable-feature while_expr"]

    [invoke same-members "erl_features:enabled()" "[cond_expr,maps,while_expr]"]

    !erl_features:keywords().
    ??['while','until']
    ?$ERLPROMPT

[shell erl]
    [invoke quit-erl]

    [invoke start-erl ""]
    !while.
    ?while
    ?$ERLPROMPT

    # If there is no Meta chunk, we should just get [] as answer,
    # not a failure.  We use a file compiled with an older release to be
    # sure no meta chunk is present
    !erl_features:used(older).
    ???[]
    ?$ERLPROMPT

    !erl_features:used("older.beam").
    ???[]
    ?$ERLPROMPT


    # Test getting used features for unknown modules and files
    !erl_features:used(no_module).
    ??not_found
    ?$ERLPROMPT

    !erl_features:used("none.beam").
    ??not_found
    ?$ERLPROMPT

[cleanup]
    [invoke no-dump]
